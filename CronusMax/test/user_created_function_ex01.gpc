int RF_HOLD = 40;
int RF_NULL = 30;

main {
    if(myfunction()) { //If myfunction returns a value other than 0(zero)...
        if(get_val(XB1_RT)) { //If we get a value from RT / R2 other than 0
            combo_run(Rapid_Fire);  //Run combo Rapid_Fire
        }
    }
}

combo Rapid_Fire {
    set_val(XB1_RT, 100);
    wait(RF_HOLD);
    set_val(XB1_RT, 0);
    wait(RF_NULL);
    set_val(XB1_RT, 0);
}

function myfunction() {
    if(get_val(XB1_VIEW)) { //If we get a value from View other than 0
        if(get_val(XB1_A))
            RF_HOLD = adjust_speed(RF_HOLD, 10, 1000, 10);
        if(get_val(XB1_B))
            RF_NULL = adjust_speed(RF_NULL, 10, 1000, 10);
        set_val(XB1_A, 0);
        set_val(XB1_B, 0);
        set_val(XB1_LB, 0);
        set_val(XB1_RB, 0);
        set_val(XB1_VIEW, 0);
        set_val(TRACE_1, RF_HOLD / 10);
        set_val(TRACE_2, RF_NULL / 10);
        return 0; //Return 0 
    }
    return 1; //If we do not get a value from View, return 1
}

function adjust_speed(var, min_value, max_value, adjustment_increment) {
    if(event_press(XB1_RB) && var < max_value) 
        var = var + adjustment_increment;
    if(event_press(XB1_LB) && var > min_value)
        var = var - adjustment_increment;
    return var;
}
